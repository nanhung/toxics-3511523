################################################################################ 
# gPYR_PBPK_ss.model
# 
# Steady state solution of metabolites for pyrethroid PBPK model
#
# 0: Deltamethrin
# 1: cis-cyfluthrin
# 2: trans-cyfluthrin
# 3: cis-cypermethrin
# 4: trans-cypermethrin
# 5: cis-permethrin
# 6: trans-permethrin
################################################################################


Outputs = {
  AUri3PBAng,  # amount of 3PBA in urine (ng/kg BW/day)
  AUriFPBAng,  # amount of 4F3PBA in urine (ng/kg BW/day)
  AUriDBCAng,  # amount of DBCA in urine (ng/kg BW/day)
  AUriDCCAcng, # amount of cis-DCCA in urine (ng/kg BW/day)
  AUriDCCAtng, # amount of trans-DCCA in urine (ng/kg BW/day)
  CUri3PBA,    # concentration of 3PBA in urine (ng/ml or ug/L)
  CUriFPBA,    # concentration of FPBA in urine (ng/ml or ug/L)
  CUriDBCA,    # concentration of DBCA in urine (ng/ml or ug/L)
  CUriDCCAc,   # concentration of cis-DCCA in urine (ng/ml or ug/L)
  CUriDCCAt,   # concentration of trans-DCCA in urine (ng/ml or ug/L)
  CUri3PBA_ND,  # Non-detected 3PBA in urine (ng/ml or ug/L)
  CUriFPBA_ND,  # Non-detected FPBA in urine (ng/ml or ug/L)
  CUriDBCA_ND,  # Non-detected DBCA in urine (ng/ml or ug/L)
  CUriDCCAc_ND, # Non-detected cis-CDDA in urine (ng/ml or ug/L)
  CUriDCCAt_ND  # Non-detected trans-3PBA in urine (ng/ml or ug/L)
};


# Physiological parameter
#-------------------------
BW; # Bodyweight (kg)
DailyCreatinine; # based on bayesmarker package (mg creatinine per day)
UrineCreatinine; # Wambaugh et al. 2013 (mg creatinine per dl urine)

# Exposure parameter (mg/kg BW /day)
lnDLM_IngDose = -27.6; # 1e-10
lnCYF_IngDose = -27.6;
lnPRM_IngDose = -27.6;
lnCPM_IngDose = -27.6;

# Molecular weight (g/mole)  
MWDLM = 505.2;
MWPRM = 391.3;
MWCPM = 416.3;
MWCYF = 434.3;

MW3PBA = 214.22;
MWFPBA = 232.21;
MWDBCA = 297.97;
MWDCCA = 209.07;

# isomer composition 
ratio_cisPRM = 0.40;
ratio_cisCPM = 0.42;
ratio_cisCYF = 0.40;

# Ratio of cypermethrin and permethrin
CPM_PRM = 0.1;

# Uptake rate constant (Quindroit et., 2019)
KA[0] = 1.51;
KA[1] = 0.52;
KA[3] = 0.52;
KA[5] = 0.52;
KA[2] = 1.3;
KA[4] = 1.3;
KA[6] = 1.3;

# Fecal excretion rate (Quindroit et., 2019)
KFec[0] = 0.59;
KFec[1] = 0.39;
KFec[2] = 0.85;
KFec[3] = 0.39;
KFec[4] = 0.85;
KFec[5] = 0.39;
KFec[6] = 0.85;

# Oral bioavaliability
F[0-6];

# Metabolic fraction
DLM_DBCA = 0.73;
cCF_FPBA = 0.10;
tCF_FPBA = 0.23;
DLM_3PBA = 0.15;
cCM_3PBA = 0.16;
tCM_3PBA = 0.39;
cPM_3PBA = 0.37;
tPM_3PBA = 0.85;
cCF_DCCA = 0.27;
cCM_DCCA = 0.32;
cPM_DCCA = 0.37;
tCF_DCCA = 0.35;
tCM_DCCA = 0.57;
tPM_DCCA = 0.61;


# Hyper parameter
#----------------------------- 
## population variance 
V_lnDLM_IngDose;
V_lnCYF_IngDose;
V_lnPRM_IngDose;
V_lnCPM_IngDose;

## Measurment error for >LOD
Ve_CUri3PBA;
Ve_CUriFPBA;
Ve_CUriDBCA;
Ve_CUriDCCAc;
Ve_CUriDCCAt;

## Measurment error for <LOD
Ve_CUri3PBA_ND;
Ve_CUriFPBA_ND;
Ve_CUriDBCA_ND;
Ve_CUriDCCAc_ND;
Ve_CUriDCCAt_ND;


Initialize {

  lnCPM_IngDose = log(exp(lnPRM_IngDose) * CPM_PRM);
  
  F[0] = KA[0]/(KA[0]+KFec[0]);
  F[1] = KA[1]/(KA[1]+KFec[1]);
  F[2] = KA[2]/(KA[2]+KFec[2]);
  F[3] = KA[3]/(KA[3]+KFec[3]);
  F[4] = KA[4]/(KA[4]+KFec[4]);
  F[5] = KA[5]/(KA[5]+KFec[5]);
  F[6] = KA[6]/(KA[6]+KFec[6]);

} # End of model initialization


CalcOutputs { 
  
  AUri3PBAng = ((exp(lnDLM_IngDose) * DLM_3PBA * F[0] * MW3PBA/MWDLM) +  
                  (exp(lnCPM_IngDose) * ratio_cisCPM * F[3] * cCM_3PBA * MW3PBA/MWCPM) + 
                  (exp(lnPRM_IngDose) * ratio_cisPRM * F[5] * cPM_3PBA * MW3PBA/MWPRM ) + 
                  (exp(lnCPM_IngDose) * (1-ratio_cisCPM ) * F[4] * tCM_3PBA * MW3PBA/MWCPM)+
                  (exp(lnPRM_IngDose) * (1-ratio_cisPRM) * F[6] * tPM_3PBA * MW3PBA/MWPRM)) * BW *1e6;
  
  AUriFPBAng = ((exp(lnCYF_IngDose) * ratio_cisCYF * F[1] * cCF_FPBA * MWFPBA/MWCYF) + 
                  (exp(lnCYF_IngDose) * (1-ratio_cisCYF) * F[2] * tCF_FPBA * MWFPBA/MWCYF)) * BW * 1e6;
  
  AUriDBCAng = exp(lnDLM_IngDose) * F[0] * DLM_DBCA * (MWDBCA/MWDLM) * BW * 1e6;
  # UEF = 36.24/(36.24+14.16) * 0.73 * (297.97/505.2) = 0.31
  
  AUriDCCAcng = ((exp(lnPRM_IngDose) * ratio_cisPRM * F[5] * cPM_DCCA * MWDCCA / MWPRM) + 
                   (exp(lnCPM_IngDose) * ratio_cisCPM * F[3] * cCM_DCCA * MWDCCA / MWCPM) + 
                   (exp(lnCYF_IngDose) * ratio_cisCYF * F[1] * cCF_DCCA * MWDCCA / MWCYF)) * BW * 1e6;
  
  AUriDCCAtng = ((exp(lnPRM_IngDose) * (1-ratio_cisPRM) * F[6] * tPM_DCCA * MWDCCA / MWPRM) + 
                   (exp(lnCPM_IngDose) * (1-ratio_cisCPM) * F[4] * tCM_DCCA * MWDCCA / MWCPM) + 
                   (exp(lnCYF_IngDose) * (1-ratio_cisCYF) * F[2] * tCF_DCCA * MWDCCA / MWCYF)) * BW * 1e6;
  
  CUri3PBA = AUri3PBAng / DailyCreatinine * (UrineCreatinine / 100); 
  CUriFPBA = AUriFPBAng / DailyCreatinine * (UrineCreatinine / 100); 
  CUriDBCA = AUriDBCAng / DailyCreatinine * (UrineCreatinine / 100); 
  CUriDCCAc = AUriDCCAcng / DailyCreatinine * (UrineCreatinine / 100); 
  CUriDCCAt = AUriDCCAtng / DailyCreatinine * (UrineCreatinine / 100);
  CUriFPBA_ND = CUriFPBA; 
  CUriDBCA_ND = CUriDBCA;
  CUri3PBA_ND = CUri3PBA;
  CUriDCCAc_ND = CUriDCCAc;
  CUriDCCAt_ND = CUriDCCAt;

} 

End.

